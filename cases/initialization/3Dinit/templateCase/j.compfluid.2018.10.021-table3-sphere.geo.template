// SPHERE

// Parameters to Determine center and radius
x = 0.;
y = 0.;
z = 0.;

r = 0.5;

// Control maximum element size
// Parameterized by j.compfluid.2018.10.021-table3.parameter
elemsize = @!TRIANGLE_EDGE_LENGTH!@;

// Define support points
// Center point
Point(1) = {x, y, z};

// Support points in plane parallel to x-y-plane through center
Point(2) = {x-r, y, z};
Point(3) = {x, y-r, z};
Point(4) = {x+r, y, z};
Point(5) = {x, y+r, z};

// Support points in plane parallel to x-z-plane through center
Point(6) = {x, y, z-r};
Point(7) = {x, y, z+r};

// Define circle arcs in plane parallel to x-y-plane
Circle(1) = {2, 1, 3};
Circle(2) = {3, 1, 4};
Circle(3) = {4, 1, 5};
Circle(4) = {5, 1, 2};

// Define circle arcs in plane parallel to x-z-plane
Circle(5) = {6, 1, 4};
Circle(6) = {4, 1, 7};
Circle(7) = {7, 1, 2};
Circle(8) = {2, 1, 6};

// Define circle arcs in plane parallel to y-z-plane
Circle(9) = {6, 1, 3};
Circle(10) = {3, 1, 7};
Circle(11) = {7, 1, 5};
Circle(12) = {5, 1, 6};

// Define Sphere segments' boundaries:
// Begin in positive sector, iterate counter clockwise, segments with x >= 0
Line Loop(1) = {3, -11, -6};
Line Loop(2) = {3, 12, 5};
Line Loop(3) = {-2, -9, 5};
Line Loop(4) = {-2, 10, -6};

// Begin in sector (x < 0, y > 0, z > 0) and iterate counter clockwise
Line Loop(5) = {-4, -11, 7};
Line Loop(6) = {-4, 12, -8};
Line Loop(7) = {1, -9, -8};
Line Loop(8) = {1, 10, 7};

// Define sphere surface patches
Surface(1) = {1} In Sphere {1};
Surface(2) = {2} In Sphere {1};
Surface(3) = {3} In Sphere {1};
Surface(4) = {4} In Sphere {1};
Surface(5) = {5} In Sphere {1};
Surface(6) = {6} In Sphere {1};
Surface(7) = {7} In Sphere {1};
Surface(8) = {8} In Sphere {1};

// Test to orient normal vectors consistently
// They should now point inwards
Reverse Surface {1};
Reverse Surface {3};
Reverse Surface {6};
Reverse Surface {8};

// Combine to sphere surface
Physical Surface(1) = {1, 2, 3, 4, 5, 6, 7, 8};

Mesh.CharacteristicLengthMax = elemsize;
Mesh.Algorithm = 6; // Frontal
